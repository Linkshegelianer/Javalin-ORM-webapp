plugins {
    id 'application'
    id 'checkstyle'
    id 'jacoco'
    id 'io.ebean' version '13.6.2'
}

group 'hexlet.code'
version '1.0-SNAPSHOT'

compileJava {
    options.release = 17
    options.encoding = 'UTF-8'
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

application {
    mainClass = 'hexlet.code.App'
}

jacocoTestReport {
    //dependsOn test // tests are required to run before generating the report
    reports {
        xml.required = true
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'io.javalin:javalin:4.6.7'
    implementation 'org.slf4j:slf4j-simple:2.0.6'

    implementation 'com.h2database:h2:2.1.214'
    implementation 'org.postgresql:postgresql:42.3.8'

    implementation 'org.thymeleaf:thymeleaf:3.0.15.RELEASE'
    implementation 'nz.net.ultraq.thymeleaf:thymeleaf-layout-dialect:3.2.0'
    implementation 'org.thymeleaf.extras:thymeleaf-extras-java8time:3.0.4.RELEASE' //добавляет объект temporal
    implementation 'org.webjars:bootstrap:5.2.3'

    implementation 'io.ebean:ebean:13.6.2'
    implementation 'io.ebean:ebean-querybean:13.6.2'
    implementation 'io.ebean:ebean-annotation:8.3'
    implementation 'io.ebean:ebean-ddl-generator:13.6.2'
    implementation 'io.ebean:ebean-migration:13.6.2'
    annotationProcessor 'io.ebean:querybean-generator:13.6.2'

    implementation 'org.glassfish.jaxb:jaxb-runtime:2.3.5'
    implementation 'javax.activation:activation:1.1.1'

    implementation 'com.konghq:unirest-java:3.13.13'

    implementation 'org.jsoup:jsoup:1.15.4'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.9.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.2'
    testImplementation 'io.ebean:ebean-test:13.6.2'
    testImplementation 'com.squareup.okhttp3:mockwebserver:4.10.0'
}

test {
    useJUnitPlatform()
    //finalizedBy jacocoTestReport // report is always generated after tests run
}

sourceSets.main.java.srcDirs = ['src/main/java', 'build/generated/sources/annotationProcessor/java/main']

task generateMigrations(type: JavaExec) {
    classpath = sourceSets.main.runtimeClasspath
    mainClass = 'hexlet.code.MigrationGenerator'
}

task stage(dependsOn: ['clean', 'installDist'])
installDist.mustRunAfter clean
